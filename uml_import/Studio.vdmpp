class Studio is subclass of Place
	instance variables
	  rehearsal: bool;
	  recording: bool;
	  inv rehearsal <> recording;
	  
	operations
		-- studio constructor: the arguments are: a place owner, the location (on a string), the stage area (in m2)
		-- and twoo booleans, indicating if its rehearsal or a recording studio
		public Studio : PlaceOwner * seq of char * nat1 * bool * bool  ==> Studio
		Studio(po, loc, sa, reh, rec) == (
			placeOwner := po;
			location := loc;
			stageArea := sa;
			rehearsal := reh;
			recording := rec;
			AdvertisingBoard`addPlace(self);
			return self;
		)
		pre reh <> rec
		post rehearsal <> recording;
		
		-- sets the studio to rehearsal
		public setRehearsal: () ==> ()
		setRehearsal() == (
			atomic(
				rehearsal := true;
				recording := false;
			);
		)
		post rehearsal = true;
		
		-- sets the studio to recording
		public setRecording: () ==> ()
		setRecording() == (
			atomic(
				rehearsal := false;
				recording := true;
			);
		)
		post recording = true;
		
		-- returns true if the place is a rehearsal studio, false otherwise
		public isRehearsal: () ==> bool
		isRehearsal() == (
			return rehearsal;
		);
		
		-- returns true if the place is a recording studio, false otherwise
		public isRecording: () ==> bool
		isRecording() == (
			return recording;
		);

end Studio
