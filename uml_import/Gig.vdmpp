class Gig
	instance variables
	  performingBands: set of (Band);
	  venue: Venue;
	  date: JyveUtils`Date;
	  
	  inv bandIsConsistent();
	  inv venueIsConsistent();
	
	operations
		public Gig : Venue * JyveUtils`Date ==> Gig
		Gig(v, d) == (
			dcl result: bool, venueGigs: set of Gig := v.gigs;
			venueGigs := venueGigs union {self};
			performingBands:= {};
			date:= d;
			atomic(
				venue:= v;
				result := v.setGigs(venueGigs);
			);
			return self;
		);
		
	  public addBand : Band  ==> bool
	  addBand(b) == (
	  	dcl complete: bool := false;
	  	dcl bandGigs: set of Gig := b.gigs;
	  	bandGigs := bandGigs union {self};
	  	
	  	atomic(
	  		performingBands := performingBands union {b};
	  		complete := b.setGigs(bandGigs);
	  	);
	  	
	  	return complete;
	  )
	  pre b not in set performingBands
	  post b in set performingBands
	  		and RESULT;
	
		pure public getDate: () ==> JyveUtils`Date
		getDate() == (
			return date;
		)
		post date = date~ and RESULT = date;
		
		pure public getVenue: () ==> Venue
		getVenue() == (
			return venue;
		)
		post venue = venue~ and RESULT = venue;
		
		public getBands: () ==> set of Band
		getBands() == (
			return performingBands;
		)
		post performingBands = performingBands~ and RESULT = performingBands;
		
		pure private bandIsConsistent: () ==> bool
		bandIsConsistent() == (
			return forall pb in set performingBands &
		  		  self in set pb.gigs;
		);
		
		pure private venueIsConsistent: () ==> bool
		venueIsConsistent() == (
			return self in set venue.gigs;
		);
		
end Gig
