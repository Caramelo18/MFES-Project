class Gig
	instance variables
	  performingBands: set of (Band);
	  venue: Venue;
	  date: Utilitys`Date;
	  
	  inv bandIsConsistent();
	  inv venueIsConsistent();
	
	operations
		public Gig : Venue * Utilitys`Date ==> Gig
		Gig(v, d) == (
			dcl result: bool, venueGigs: set of Gig := v.gigs;
			venueGigs := venueGigs union {self};
			performingBands:= {};
			atomic(
				venue:= v;
				result := v.setGigs(venueGigs);
			);
			date:= d;
			return self;
		);
		
	  public addBand : Band  ==> bool
	  addBand(b) == (
	  	dcl complete: bool := false;
	  	dcl bandGigs: set of Gig := b.gigs;
	  	bandGigs := bandGigs union {self};
	  	
	  	atomic(
	  		performingBands := performingBands union {b};
	  		complete := b.setGigs(bandGigs);
	  	);
	  	
	  	return complete;
	  )
	  pre b not in set performingBands
	  post b in set performingBands
	  		and RESULT;
	
		pure public getDate: () ==> Utilitys`Date
		getDate() == (
			return date;
		);
		
		public getBands: () ==> set of Band
		getBands() == (
			return performingBands;
		)
		post performingBands = performingBands~ and RESULT = performingBands;
		
		pure private bandIsConsistent: () ==> bool
		bandIsConsistent() == (
			return forall pb in set performingBands &
		  		  self in set pb.gigs;
		);
		
		pure private venueIsConsistent: () ==> bool
		venueIsConsistent() == (
			return self in set venue.gigs;
		);
		
end Gig
