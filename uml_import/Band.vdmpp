class Band is subclass of Profile		
	instance variables
	  genres:set of (JyveUtils`Genre);
	  public gigs: set of Gig;
	  
	  inv not exists g1, g2 in set gigs &
	  		g1 <> g2 and g1.getDate() = g2.getDate();
	
	operations
		public Band: String ==> Band
		Band(n) == (
			name:= n;
			genres:= {};
			gigs:= {};
			return self;
		);
		
		public getGenres: () ==> set of JyveUtils`Genre
		getGenres() == (
			return genres;
		);
		
	  public  addGenre : JyveUtils`Genre  ==> ()
	  addGenre(g) == (
	  	genres:= genres union {g};
	  )
	  pre g not in set genres
	  post g in set genres;
	
	  public  deleteGenre : JyveUtils`Genre  ==> ()
	  deleteGenre(g) == (
	  	genres:= genres \ {g};
	  )
	  pre g in set genres
	  post g not in set genres;
	  	  
	  public getGigs: () ==> set of Gig
	  getGigs() == (
	  	return gigs;
	  )	
	  post gigs = gigs~ and RESULT = gigs;
	  
	  public setGigs: set of Gig ==> bool
	  setGigs(g) == (
	  	gigs := g;
	  	return true;
	  )
	  post gigs = g;

end Band
